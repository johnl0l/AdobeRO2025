//===== rAthena Script =======================================
//= Job Master
//===== Description: =========================================
//= A fully functional job changer.
//===== Additional Comments: =================================
//= 1.0 Initial script. [Euphy]
//= 1.1 Fixed reset on Baby job change.
//= 1.2 Added Expanded Super Novice support and initial Kagerou/Oboro support.
//= 1.3 Kagerou/Oboro added.
//= 1.4 Rebellion added.
//= 1.5 Added option to disable RebirthClass. [mazvi]
//= 1.6 Added option to get job related equipment on change. [Braniff]
//= 1.7 Readability changes. Also added BabyExpanded and BabySummoner classes. [Jey]
//= 1.8 Added option to disable Baby Novice Only but Baby Class can be Enabled [mazvi]
//= 1.9 Migrate/Integrate to Global Functions Platinum Skills. [mazvi]
//============================================================

prontera,158,193,6	script	Job Master	123,{
function Get_Job_Equip;
// Checks if the Player has the required level.
// closes if not, returns if yes
function	Require_Level	{
	if (BaseLevel < getarg(0) || JobLevel < getarg(1)) {
		.@blvl = getarg(0) - BaseLevel;
		.@jlvl = getarg(1) - JobLevel;
		mes "Level requirement:";
		mes ((getarg(0)>1)? 
			"^bb0000"+getarg(0)+"^000000 (^bb0000Base^000000) / ":"")+"^00bb00"+
			getarg(1)+"^000000 (^00bb00Job^000000)";
		mes "You need " +
			((.@blvl > 0) ? "^bb0000"+.@blvl+"^000000 more base levels " + 
				((.@jlvl > 0) ? "and " : "") : "") +
			((.@jlvl > 0) ? "^00bb00"+.@jlvl+"^000000 more job levels " : "") +
			"to continue.";
		close;
	}
	return;
}

// Checks if the given eac is a baby class
function Is_Baby	{
	return ((getarg(0, eaclass())&EAJL_BABY)>0);
}

// Checks if the player can change to third class.
// Note: This does not include the level checks.
function	Can_Change_Third	{
	// To change to third class you either need to be:
	// * Second Class
	// * Transcendent Second Class
	// * Baby Second Class
	if( !.ThirdClass )
		return false; // Third job change disabled
	if( !(eaclass()&EAJL_2) )
		return false; // Not second Class
	if( eaclass()&EAJL_THIRD )
		return false; // Already Third Class
	if( roclass(eaclass()|EAJL_THIRD) < 0 )
		return false; // Job has no third Class
	if( (eaclass()&EAJ_UPPERMASK) == EAJ_SUPER_NOVICE )
		return false; // Exp. Super Novice equals 3rd Cls, but has it's own case
	if( Is_Baby() && (!.BabyClass || !.BabyThird) )
		return false; // No Baby (Third) change allowed
	return true;
}

function	Can_Rebirth	{
	// To rebirth, you need to be:
	// * Second Class
	if( !.RebirthClass )
		return false; // Rebirth disabled
	if( !(eaclass()&EAJL_2) )
		return false; // Not second Class
	if( eaclass()&(EAJL_UPPER|EAJL_THIRD) )
		return false; // Already Rebirthed/ Third Class
	if( roclass(eaclass()|EAJL_UPPER) < 0 )
		return false; // Job has no transcended class
	if( Is_Baby() && !.BabyClass )
		return false; // No Baby changes allowed
	return true;
}

// Checks if the given eac is a first class
function	Is_First_Cls	{
	return (getarg(0) <= EAJ_TAEKWON);
}

function	Check_Riding	{
	// Note: Why we should always check for Riding:
	// Mounts are considered as another class, which
	// would make this NPC bigger just to handle with
	// those special cases.
	if (checkfalcon() || checkcart() || checkriding() || ismounting()) {
		mes "Please remove your ^ff0000" +
			((checkfalcon()) ? "falcon" : "") +
			((checkcart()) ? "cart" : "") +
			((checkriding()) ? "Peco" : "") +
			((ismounting()) ? "mount" : "") +
			"^000000 before proceeding.";
		close;
	}
	return;
}
function	Check_SkillPoints	{
	if (.SkillPointCheck && SkillPoint) {
		mes "Please use all your ^ff0000skill points^000000 before proceeding.";
		close;
	}
	return;
}

// addJobOptions is essentially the same like
// setarray .@array[getarraysize(.@array)],opt1,opt2,...;
// It's just easier to read, since we're using it very often
function	Job_Options	{
	.@argcount = getargcount();
	.@arr_size = getarraysize(getarg(0));
	for( .@i = 1; .@i < .@argcount; .@i++) {
		setarray getelementofarray(getarg(0), .@arr_size++),getarg(.@i);
	}
}

// Begin of the NPC
	mes .NPCName$;
	Check_Riding();
	Check_SkillPoints();

	// initialisation
	.@eac = eaclass();
	.@third_possible = Can_Change_Third();
	.@rebirth_possible = Can_Rebirth();
	.@first_eac = .@eac&EAJ_BASEMASK;
	.@second_eac = .@eac&EAJ_UPPERMASK;
	// Note: These are already set in pc.cpp
	// BaseClass = roclass(.@eac&EAJ_BASEMASK) which is the players First Class
	// BaseJob = roclass(.@eac&EAJ_UPPERMASK) which is the players Second Class
	//dispbottom "Debug: eac ("+.@eac+"), third ("+.@third_possible+"), rebirth("+.@rebirth_possible+"), BaseClass ("+BaseClass+"), BaseJob ("+BaseJob+")";
		
	// From here on the jobmaster checks the current class
	// and fills the the array `.@job_opt` with possible
	// job options for the player.
	
	if( .@rebirth_possible ) {
		// Rebirth option (displayed on the top of the menu)
		Require_Level(.Req_Rebirth[0], .Req_Rebirth[1]);
		Job_Options(.@job_opt, Job_Novice_High);
	}
	if( .@third_possible ) {
		// Third Job change (displayed below rebirth)
		Require_Level(.Req_Third[0], .Req_Third[1]);
		Job_Options(.@job_opt, roclass(.@eac|EAJL_THIRD));
	}
	 
	if (.SecondExpanded && 
	   (.@eac&EAJ_UPPERMASK) == EAJ_SUPER_NOVICE && // is Super Novice
	   !(eaclass()&EAJL_THIRD) ) {				 	// not already Expanded SN
		// (Baby) Super Novice to Expanded (Baby) Super Novice
	   	if( !Is_Baby(.@eac) || (.BabyClass && .BabyExpanded) ) {
			// .BabyClass & .BabyExpanded must be enabled if the is a baby
			Require_Level(.Req_Exp_SNOVI[0], .Req_Exp_SNOVI[1]);
			Job_Options(.@job_opt,roclass(.@eac|EAJL_THIRD)); // Expanded SN is "third" cls
		}
	}
	
	if (.SecondExpanded && 
		((.@eac&(~EAJL_BABY)) == EAJ_NINJA || 		// is (Baby) Ninja
		(.@eac&(~EAJL_BABY)) == EAJ_GUNSLINGER)) {	// is (Baby) Gunslinger
		// (Baby) Ninja to (Baby) Kagerou / Oboro
		// (Baby) Gunslinger to (Baby) Rebellion
		if( !Is_Baby(.@eac) || (.BabyClass && .BabyExpanded) ) {
			// .BabyClass & .BabyExpanded must be enabled if the is a baby
			Require_Level(.Req_Exp_NJ_GS[0], .Req_Exp_NJ_GS[1]);
			// Kagerou, Oboro, Rebellion are considered as a 2-1 class
			Job_Options(.@job_opt, roclass(.@eac|EAJL_2_1));
		}
	}
	
	// Player is Job_Novice, Job_Novice_High or Job_Baby
	if (.@first_eac == EAJ_NOVICE && .@second_eac != EAJ_SUPER_NOVICE) {
		// MAPID_NOVICE, MAPID_SUPER_NOVICE, MAPID_NOVICE_HIGH, MAPID_BABY
		Require_Level(.Req_First[0], .Req_First[1]);
		switch(Class) {
			case Job_Novice:
				// First job change
				Job_Options(.@job_opt,Job_Swordman,
					Job_Mage, Job_Archer, Job_Acolyte, Job_Merchant, Job_Thief,
					Job_Super_Novice, Job_Taekwon, Job_Gunslinger, Job_Ninja);
				if( .BabyNovice )
					Job_Options(.@job_opt, Job_Baby);
				break;
			case Job_Novice_High:
				// Job change after rebirth
				if( .LastJob && lastJob )
					Job_Options(.@job_opt,
						roclass((eaclass(lastJob)&EAJ_BASEMASK)|EAJL_UPPER));
				else
					Job_Options(.@job_opt, 
						Job_Swordman_High, Job_Mage_High, Job_Archer_High,
						Job_Acolyte_High, Job_Merchant_High, Job_Thief_High);
				break;
			case Job_Baby:
				if( !.BabyClass )
					break;
				// First job change as a baby
				Job_Options(.@job_opt, Job_Baby_Swordman, Job_Baby_Mage,
					Job_Baby_Archer,Job_Baby_Acolyte, Job_Baby_Merchant,
					Job_Baby_Thief);
				if( .BabyExpanded )
					Job_Options(.@job_opt, Job_Super_Baby, Job_Baby_Taekwon,
						Job_Baby_Gunslinger, Job_Baby_Ninja);
				if( .BabySummoner )
					Job_Options(.@job_opt, Job_Baby_Summoner);
				break;
			default:
				mes "An error has occurred.";
				close;
		}
	} else if( Is_First_Cls(.@eac) || 				// First Class
			   Is_First_Cls(.@eac&(~EAJL_UPPER)) ||	// Trans. First Cls
			   (.BabyClass && Is_First_Cls(.@eac&(~EAJL_BABY))) ) {	// Baby First Cls
		// Player is First Class (not Novice)
		// most jobs should have two options here  (2-1 and 2-2)
		.@class1 = roclass(.@eac|EAJL_2_1); // 2-1
		.@class2 = roclass(.@eac|EAJL_2_2); // 2-2
		// dispbottom "Debug: Classes: class1 ("+.@class1+"), class2 ("+.@class2+")";
		if(.LastJob && lastJob && (.@eac&EAJL_UPPER)) {
			// Player is rebirth Cls and linear class changes are enforced
			Require_Level(.Req_Second[0], .Req_Second[1]);
			Job_Options(.@job_opt, lastJob + Job_Novice_High);
		} else {
			// Class is not enforced, player can decide.
			if( .@class1 > 0 ) { // 2-1
				Require_Level(.Req_Second[0], .Req_Second[1]);
				Job_Options(.@job_opt, .@class1);
			}
			if( .@class2 > 0 ) { // 2-2
				Require_Level(.Req_Second[0], .Req_Second[1]);
				Job_Options(.@job_opt, .@class2);
			}
		}
	}
	
	// Displaying the Job Menu defined by .@job_opt.
	// .@job_opt should not be changed below this line.
	function Job_Menu;
	Job_Menu(.@job_opt);
	close;

// Displays the job menu
function	Job_Menu	{
	function Confirm_Change;
	while(true) {
			mes "I can train you to get stronger ^008aff"+strcharinfo(0)+"^000000!.";
			mes "You don't even have to complete a quest!";
			mes "...";
			next;
			mes .NPCName$;
			mes "^DAA520Prices^000000:^ff0000$^000000";
			mes "- ^8FBC8FFirst Class^000000 = ^ff0000"+.cost[0]+"^000000z";
			mes "- ^3CB371Second Class^000000 = ^ff0000"+.cost[1]+"^000000z";
			//mes "- ^2E8B57Third Class^000000 = ^ff0000"+.cost[2]+"^000000z";
			mes "- ^2E8B57Rebirth^000000 = ^008B8B"+getitemname(.rebirthRequiredItemId[0])+"^000000 & ^ff0000"+.cost[3]+"^000000z.";
			next;
	Check_Riding();
	Check_SkillPoints();
	// getarg(0) is the .@job_opt array holding all available job changes.
		.@opt_cnt =  getarraysize(getarg(0));
		if( .@opt_cnt <= 0 ) {
			mes .NPCName$;
			mes "No more ^ff0000jobs^000000 are ^ff0000available^000000.";
			close;
		}

		.@selected = 0; // Just a single job class given, no select needed
		if (.@opt_cnt > 1) {
			// Multiple job classes given. Select one and save it to .@class
			// After that confirm .@class
			mes .NPCName$;
			mes "Select a job.";
			.@menu$ = "";
			for (.@i = 0; .@i < .@opt_cnt; .@i++) {
				if( getelementofarray(getarg(0), .@i) == Job_Novice_High)
					.@jobname$ = "^0055FFRebirth^000000";
				else
					.@jobname$ = jobname(getelementofarray(getarg(0), .@i));
				.@menu$ = .@menu$ + .bcor$+" " + .@jobname$ + ":";
			}
			.@menu$ = .@menu$+ .rcor$+" ^777777Cancel^000000";
			.@selected = select(.@menu$) - 1;
			if( .@selected < 0 || .@selected >= .@opt_cnt )
				close;
			next;

		}
		.@class = getelementofarray(getarg(0), .@selected);
		if ((.@class == Job_Super_Novice || .@class == Job_Super_Baby) &&
				BaseLevel < .SNovice) {
			// Special Level Requirement because Super Novice and
			// Super Baby can both be selected in one of the first class
			// changes. That's why the Level Requirement is after and not before
			// the selection.
			mes .NPCName$;
			mes "A base level of ^ff0000" + .SNovice +
				"^000000 is required to turn into a ^008aff" + jobname(.@class) + "^000000.";
			return;
		}
		// Confirm the Class
		Confirm_Change(.@class, .@opt_cnt > 1);
		next;
		mes .NPCName$;
	}
	return;
}


// Executes the actual jobchange and closes.
function	Job_Change	{

	.@previous_class = Class;
	.@to_cls = getarg(0);
	
	if ( (.@to_cls >= Job_Swordman && .@to_cls <= Job_Thief) || (.@to_cls >= Job_Gunslinger && .@to_cls <= Job_Ninja) || .@to_cls == Job_Taekwon || (.@to_cls >= Job_Swordman_High && .@to_cls <= Job_Thief_High) || (.@to_cls >= Job_Baby && .@to_cls <= Job_Baby_Thief) || .@to_cls == Job_Baby_Gunslinger || .@to_cls == Job_Baby_Ninja || .@to_cls == Job_Baby_Taekwon )	{
		.costamount = .cost[0];
	} else if ( (.@to_cls >= Job_Knight && .@to_cls <= Job_Crusader2) || (.@to_cls >= Job_Lord_Knight && .@to_cls <= Job_Paladin2) || (.@to_cls >= Job_Star_Gladiator && .@to_cls <= Job_Soul_Linker) || (.@to_cls >= Job_Kagerou && .@to_cls <= Job_Rebellion) || .@to_cls == Job_SuperNovice || (.@to_cls >= Job_Baby_Knight && .@to_cls <= Job_Super_Baby) )	{
		.costamount = .cost[1];
	} else {
		.costamount = .cost[2];
	}
	if ( (.@to_cls == Job_Novice_High && .LastJob) )  {
		.costamount = .cost[3];
	}
	
	if ( Zeny < .costamount )	{
		next;
		mes .NPCName$;
		mes "You don't have ^ff0000"+.costamount+"^000000 Zeny!";
		close;
	}
	
	next;
	mes .NPCName$;
	mes "You are now ^008aff" + callfunc("F_InsertArticle", jobname(.@to_cls)) + "^000000!";
	
	if (.@to_cls == Job_Novice_High && .LastJob) {
		lastJob = Class;		// Saves the lastJob for rebirth
		}
		Zeny -= .costamount;
		jobchange .@to_cls;
	if (.@to_cls == Job_Novice_High)
		resetlvl(1);
	else if (.@to_cls == Job_Baby) {
		resetstatus;
		resetskill;
		set SkillPoint,0;
	}
	specialeffect2 EF_ANGEL2;
	specialeffect2 EF_ELECTRIC;
	if (.@previous_class != Class) {
		if (.Platinum)
			callfunc "F_GetPlatinumSkills";
		if (.GetJobEquip)
			Get_Job_Equip();
	}
	close; // Always closes after the change
}

function	Confirm_Change	{
	// Player confirms he want to change into .@class
	.@class = getarg(0, -1);
	.@back = getarg(1, false);
	if( .@class < 0 || eaclass(.@class) == -1 ) {
		mes .NPCName$;
		mes "Unknown Class Error.";
		close;
	}
	mes .NPCName$;
	mes "Do you want to change into ^0055FF"+jobname(.@class)+"^000000 class?";
	.@job_option$ = .bcor$+" Change into ^0055FF"+jobname(.@class)+"^000000 class";
	if( .@class == Job_Novice_High)
		.@job_option$ = .bcor$+" ^0055FFRebirth^000000";
	
	if (select(.@job_option$+ ":" + 
			((.@back) ?.rcor$+"^777777Go back^000000" : .rcor$+"^777777Cancel^000000") + "") == 1) {
		if( .@class == Job_Novice_High)
		{
			if(getarraysize(.rebirthRequiredItemId) > (Class - 7)) //check if required item id have been provided
			{
				if ( Zeny < .cost[3] )	{
					next;
					mes .NPCName$;
					mes "You don't have ^ff0000" + callfunc("F_InsertComma",.cost[3]) + "^000000z!";
					close;
				}
				if(countitem(.rebirthRequiredItemId[Class - 7]) > 0)
				{
					next;
					mes .NPCName$;
					mes "I see if you have ^008B8B" + getitemname(.rebirthRequiredItemId[0]) + "^000000 + ^ff0000" + callfunc("F_InsertComma",.cost[3]) + "^000000z. ^0055FFGood^000000.";
					delitem .rebirthRequiredItemId[Class - 7], 1;
					Job_Change(.@class);
				}
				else
				{
					next;
					mes .NPCName$;
					mes "You don't have ^008B8B" + getitemname(.rebirthRequiredItemId[0]) + "^000000. Come back with it before asking for ^0055FFRebirth^000000.";
					close;
				}

			}
		}
		Job_Change(.@class);
	}
	if (!.@back)
		close; // "Cancel" pressed
	return;
}

// Function which gives a job related item to the player
// the items are the rewards from the original job change quests
function	Get_Job_Equip	{
	// Note: The item is dropping, when the player can't hold it.
	// But that's better than not giving the item at all.
	.@eac = eaclass();
	if( .@eac&EAJL_THIRD ) {
		// Third Class Items
		getitem 2795,1;	//	Green Apple Ring for every 3rd Class
		switch(BaseJob) {
			// BaseJob of Third Cls
			// For Normal Third, Baby Third and Transcended Third Cls
			case Job_Knight:
				getitem 5746,1;	break;	//	Rune Circlet [1]
			case Job_Wizard:
				getitem 5753,1;	break;	//	Magic Stone Hat [1]
			case Job_Hunter:
				getitem 5748,1;	break;	//	Sniper Goggle [1]
			case Job_Priest:
				getitem 5747,1;	break;	//	Mitra [1]
			case Job_Blacksmith:
				getitem 5749,1;	break;	//	Driver Band [1]
			case Job_Assassin:
				getitem 5755,1;	break;	//	Silent Executor [1]
			case Job_Crusader:
				getitem 5757,1;	break;	//	Dip Schmidt Helm [1]
			case Job_Sage:
				getitem 5756,1;	break;	//	Wind Whisper [1]
			case Job_Bard:
				getitem 5751,1;	break;	//	Maestro Song's Hat [1]
			case Job_Dancer:
				getitem 5758,1;	break;	//	Dying Swan [1]
			case Job_Monk:
				getitem 5754,1;	break;	//	Blazing Soul [1]
			case Job_Alchemist:
				getitem 5752,1;	break;	//	Midas Whisper[1]
			case Job_Rogue:
				getitem 5750,1; 		//	Shadow Handicraft [1]
				getitem 6121,1;			//	Makeover Brush
				getitem 6122,1;	break;	//	Paint Brush
		}
	} else if ( !(.@eac&EAJL_2) && !(.@eac&EAJL_UPPER) ) {
		// Not Second Job AND not Rebirth/Third
		if ( (.@eac&EAJ_UPPERMASK) == EAJ_SWORDMAN ) {
			getitem 1108,1;
			getitem 2104,1;
		} else if ( (.@eac&EAJ_UPPERMASK) == EAJ_MAGE ) {
			getitem 1608,1;
			getitem 2121,1;
		} else if ( (.@eac&EAJ_UPPERMASK) == EAJ_ARCHER ) {
			getitem 1705,1;
			getitem 1770,1;
		} else if ( (.@eac&EAJ_UPPERMASK) == EAJ_ACOLYTE ) {
			getitem 1502,1;
			getitem 2104,1;
		} else if ( (.@eac&EAJ_UPPERMASK) == EAJ_MERCHANT ) {
			getitem 1309,1;
			getitem 2104,1;
		} else if ( (.@eac&EAJ_UPPERMASK) == EAJ_THIEF ) {
			getitem 1217,1;
			getitem 2104,1;
		} else if ( (.@eac&EAJ_UPPERMASK) == EAJ_GUNSLINGER ) {
			getitem 13101,1;
			getitem 13200,1;
		} else if ( (.@eac&EAJ_UPPERMASK) == EAJ_NINJA ) {
			getitem 13011,1;
			getitem 2102,1;
		} else if ( (.@eac&EAJ_UPPERMASK) == EAJ_TAEKWON ) {
			getitem 2102,1;
			getitem 2404,1;
		}
	}
	return;
}

OnInit:
	// Initialisation, do not edit these
	.NPCName$ = "[ ^FF7F00Job Master^000000 ]";
	.bcor$ = "^008aff[>]^000000 ";		// Option Button Symbol;
	.rcor$ = "^ff0000[>]^000000 ";		// Cancel button symbol;
	
	// Settings
	.ThirdClass = false;			// Enable third classes?
	.RebirthClass = true;			// Enable rebirth classes?
	.SecondExpanded = false;		// Enable new expanded second classes: Ex. Super Novice, Kagerou/Oboro, Rebellion?
	.BabyNovice = true;			// Enable Baby novice classes? Disable it if you like player must have parent to get job baby.
	.BabyClass = true;			// Enable Baby classes?
	.BabyThird = false;			// Enable Baby third classes?
	.BabyExpanded = false;			// Enable Baby Expanded classes: Ex. Baby Ninja, Baby Taekwon, etc.
	.BabySummoner = false;			// Enable Baby Summoner?
	.LastJob = true;			// Enforce linear class changes?
	.SkillPointCheck = true;		// Force player to use up all skill points?
	.Platinum = false;			// Get platinum skills automatically?
	.GetJobEquip = true;			// Get job equipment (mostly weapons) on job change?

	// Level Requirements
	setarray .Req_First[0],1,10; 		// Minimum base level, job level to turn into 1st class
	setarray .Req_Second[0],1,40; 		// Minimum base level, job level to turn into 2nd class
	setarray .Req_Rebirth[0],99,50;		// Minimum base level, job level to rebirth
	setarray .Req_Third[0],99,50;		// Minimum base level, job level to change to third class
	setarray .Req_Exp_NJ_GS[0],99,70; 	// Minimum base level, job level to turn into Expanded Ninja and Gunslinger
	setarray .Req_Exp_SNOVI[0],99,99; 	// Minimum base level, job level to turn into Expanded Super Novice
	.SNovice = 45;				// Minimum base level to turn into Super Novice
	setarray .cost,				// Cost for Jobchange
	15000,					// To First Job
	25000,					// To Second Job
	1000000,				// To Third Job
	1285000;				// To Rebirth
	setarray .rebirthRequiredItemId[0],7825,7825,7825,7825,7825,7825,7825,7825,7825,7825,7825,7825,7825,7825; // Item Requeriment for Rebirth. "Heroic Stone Item ID# 7825 (Heroic_Stone)"
	
	// Setting adjustments by PACKETVER
	if( PACKETVER < 20161207 ) {
		if( .BabyExpanded )
			debugmes "jobmaster: BabyExpanded is disabled due to outdated PACKETVER.";
		if( .BabySummoner )
			debugmes "jobmaster: BabySummoner is disabled due to outdated PACKETVER.";
		.BabyExpanded = false;
		.BabySummoner = false;
	}
	end;
}
